apply plugin: 'com.android.application'
apply from: '../preprocessor.gradle'

android {
    if (project.hasProperty("MyProject.properties")
            && new File(project.property("MyProject.properties") as String).exists()) {

        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("MyProject.properties"))))

        signingConfigs {
            release {
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
            debug {
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
            versionName "1.0-Paid"
        }
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
            versionName "1.0-Free"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

repositories {
    jcenter();
    maven { url "https://jitpack.io" }
}

// Needed to resolve app vs test dependencies, specifically, transitive dependencies
configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // include the joke Android library
    compile project(':jokeandroidlibrary')
    // Added for adMob
    freeCompile 'com.google.android.gms:play-services-ads:8.4.0'
    // Test dependencies
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    // the jokeBackend
    compile project(path: ':jokeBackend', configuration: 'android-endpoints')
    // Libraries normally used by this app
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.github.jakob-grabner:Circle-Progress-View:v1.2.2'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.android.support:support-v4:23.1.1'
}
